###################################################################
# By Caio Rodrigues for UFCG photonics (2022)

# Professor: Adolfo Hebster 

# MMI Study

# Lumerical MODE MMI Sweeps
###################################################################

###################################################################
# PARAMETERS

SPar = [2,3]; # Ports to analyse S parameters (SX1)

TapperSweep = 1; # Enable tappers width sweep
TSRange = [1e-6,1.4e-6]; # Tapper sweep range
TSPoints = 15; # Tapper sweep number of points

# to-do:
# Body Width sweep
# Body Length sweep

###################################################################

# WARNING
# All visualizers will be closed after script execution

SParSz = length(SPar);
pMatrix = matrix(TSPoints,SParSz);
TRangeMatrix = linspace(TSRange(1),TSRange(2),TSPoints);

if(TapperSweep == 1) {
    selectpartial("Output Taper");
    OutP = getnumber;
    print("Tapper width sweep in progress...");
    print("Number of output ports found: " + num2str(OutP));

    for (i=1; i <= TSPoints  ;i=i+1){
        text = "Progress: " + num2str(i) + "/" + num2str(TSPoints);
        print(text);    
        
        switchtolayout;
        for(j=1; j <= OutP ;j=j+1){
            setnamed("Output Taper " +num2str(j),
            "width_l",TRangeMatrix(i));
        }
        run;
        emepropagate;
        
        A = getresult("EME","user s matrix");
        for(j=1 ; j <= SParSz ;j=j+1){
            pMatrix(i,j) = abs(A(SPar(j),1))^2;
            dMatrix(i,j) = 20*log10(abs(A(SPar(j),1)));
        }
    }
    closeall;
    P = matrixdataset("S parameters in Abs^2");
    D = matrixdataset("S parameters in dB");
    P.addparameter("Tapper width",TRangeMatrix);
    D.addparameter("Tapper width",TRangeMatrix);  
    for(i=1; i<=SParSz ;i=i+1){
        X = pMatrix(1:TSPoints, i);
        Y = dMatrix(1:TSPoints, i);
        P.addattribute("S"+num2str(SPar(i))+"1" ,X);
        D.addattribute("S"+num2str(SPar(i))+"1" ,Y);
    }
    visualize(P);
    visualize(D);
}